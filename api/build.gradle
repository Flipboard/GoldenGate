apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

configurations {
    archives {
        extendsFrom configurations.default
    }
}

version = "1.3.0"
group = "com.flipboard.goldengate"

if (canUploadArchives()) {
    uploadArchives {
        configuration = configurations.archives
        repositories.mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: sonatypeRepo) {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'GoldenGate'
                packaging 'jar'
                description 'Generate type safe java bridge into webview javascript code'
                url 'https://github.com/flipboard/GoldenGate'

                scm {
                    url 'https://github.com/Flipboard/GoldenGate.git'
                }

                licenses {
                    license {
                        name 'BSD 3-Clause License'
                        url 'https://github.com/Flipboard/GoldenGate/blob/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'emilsjolander'
                        name 'Emil SjÃ¶lander'
                        email 'esjolander@flipboard.com'
                    }
                }
            }
        }
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

afterEvaluate { project ->
    task androidJavadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += files(android.bootClasspath)
    }

    task androidJavadocJar(type: Jar) {
        classifier = 'javadoc'
        from androidJavadoc.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    artifacts {
        archives androidJavadocJar
        archives androidSourcesJar
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    provided 'com.google.code.gson:gson:2.3.1'
}
